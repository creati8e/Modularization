buildscript {

    ext.kotlin_version = '1.3.10'
    ext.nav_version = "1.0.0-alpha07"
    ext.dagger_version = "2.19"
    ext.androidx_version = "1.0.0"
    ext.material_components_version = "1.0.0"

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.5.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

configure(subprojects) {

    afterEvaluate {
        android {
            compileSdkVersion 28
            buildToolsVersion '28.0.3'

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 28
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }

    project.configurations.all {
        resolutionStrategy {
            force "androidx.cardview:cardview:$androidx_version"
            force "androidx.recyclerview:recyclerview:$androidx_version"
            force "com.google.android.material:material:$material_components_version"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
apply plugin: "com.vanniktech.dependency.graph.generator"


import com.vanniktech.dependency.graph.generator.DependencyGraphGeneratorExtension.Generator
import com.vanniktech.dependency.graph.generator.DependencyGraphGeneratorPlugin
import guru.nidi.graphviz.attribute.Color
import guru.nidi.graphviz.attribute.Style

plugins.apply(DependencyGraphGeneratorPlugin)

// Task name is generateDependencyGraphModules.
def modulesGenerator = new Generator(
        "Modules", // Postfix for our Gradle task.
        { dependency ->
            // Specify your project name here.
            def filter = dependency.getModuleGroup().startsWith("Modularization")
            if (filter) {
                System.out.println(dependency.getModule().getId().toString())
            }
            filter
        },
        { dependency -> false }, // Include transitive dependencies.
        { node, dependency -> node.add(Style.FILLED, Color.rgb("#ffcb2b")) }, // Give them some color.
)

dependencyGraphGenerator {
    generators = [modulesGenerator]
}